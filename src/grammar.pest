ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

sized_integer = {
    "u8" | "u16" | "u32" | "u64" | "f32" |
    "i8" | "i16" | "i32" | "i64" | "f64"
}
primitive = { sized_integer | "usize" | "isize" | "bool" | "string" }
buffer = { "buffer" ~ "<" ~ sized_integer ~ ">" }
tuple = { "(" ~ (type_ ~ ("," ~ type_)*)? ~ ","? ~ ")" }
ref_ = { "&" ~ type_ }
slice = { "[" ~ type_ ~ "]" }
vec = { "Vec" ~ "<" ~ type_ ~ ">" }
opt = { "Option" ~ "<" ~ type_ ~ ">" }
res = { "Result" ~ "<" ~ type_ ~ ">" }
iter = { "Iterator" ~ "<" ~ type_ ~ ">" }
fut = { "Future" ~ "<" ~ type_ ~ ">" }
stream = { "Stream" ~ "<" ~ type_ ~ ">" }
type_ = { primitive | buffer | tuple | ref_ | slice | vec | opt | res | iter | fut | stream | ident }

doc = @{ (!"\n" ~ ANY)* ~ "\n" }
module_docs = @{ "//!" ~ doc }
item_docs = @{ "///" ~ doc }
static_ = { "static" }
arg = { ident ~ ":" ~ type_ }
args = { (arg ~ ("," ~ arg)*)? }
function = { item_docs* ~ static_? ~ "fn" ~ ident ~ "(" ~ args ~ ")" ~ ("->" ~ type_)?  ~ ";" }
object = { item_docs* ~ "object" ~ ident ~ "{" ~ function* ~ "}" }

enum_inner = { "(" ~ type_ ~ ")" }
enum_entry = { ident ~ enum_inner? }
enum_ = { item_docs* ~ "enum" ~ ident ~ "{" ~ enum_entry ~ ("," ~ enum_entry)* ~ "}" }

root = { SOI ~ module_docs* ~ (object | function | enum_)* ~ EOI }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ !("/" | "!" | "\n") ~ (!"\n" ~ ANY)* ~ "\n") }
